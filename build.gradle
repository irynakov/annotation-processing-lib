buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
}

plugins {
    id "java"
    id "java-library"
    id "groovy"
    id "maven"
    id "net.researchgate.release" version "2.8.0"
    id "org.sonarqube" version "2.7"
    id 'jacoco'
}

apply from: "$rootDir/gradle/coralNexus.gradle"

group = "com.annotation.processing.lib"

repositories {
    coralNexus()
    mavenCentral()
}

targetCompatibility = 1.8
sourceCompatibility = 1.8


project.ext["release.useAutomaticVersion"] = true

release {
    preTagCommitMessage = '[Gradle Release Plugin] [skip ci] - pre tag commit: '
    tagCommitMessage = '[Gradle Release Plugin] [skip ci] - creating tag: '
    newVersionCommitMessage = '[Gradle Release Plugin] [skip ci] - new version commit: '
//    git {
//        requireBranch = "master"
//        pushToRemote = 'origin'
//    }
}

sonarqube {
    properties {
        property "sonar.scm.disabled", "true"
        property "sonar.exclusions", "**/entity/**"
    }
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

afterReleaseBuild.dependsOn uploadArchives

def skip_quality_check = project.hasProperty('check') && check == 'false'
println("skip_quality_check: " + skip_quality_check)

if (!skip_quality_check) {
    apply from: "${rootDir}/gradle/quality_config.gradle"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'
    testImplementation('org.spockframework:spock-spring:1.3-groovy-2.5')
}
